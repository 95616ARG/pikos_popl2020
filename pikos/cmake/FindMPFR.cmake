###############################################################################
#
# Find MPFR headers and libraries.
#
# Author: Maxime Arthaud
#
# Contact: ikos@lists.nasa.gov
#
# Notices:
#
# Copyright (c) 2018-2019 United States Government as represented by the
# Administrator of the National Aeronautics and Space Administration.
# All Rights Reserved.
#
# Disclaimers:
#
# No Warranty: THE SUBJECT SOFTWARE IS PROVIDED "AS IS" WITHOUT ANY WARRANTY OF
# ANY KIND, EITHER EXPRESSED, IMPLIED, OR STATUTORY, INCLUDING, BUT NOT LIMITED
# TO, ANY WARRANTY THAT THE SUBJECT SOFTWARE WILL CONFORM TO SPECIFICATIONS,
# ANY IMPLIED WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE,
# OR FREEDOM FROM INFRINGEMENT, ANY WARRANTY THAT THE SUBJECT SOFTWARE WILL BE
# ERROR FREE, OR ANY WARRANTY THAT DOCUMENTATION, IF PROVIDED, WILL CONFORM TO
# THE SUBJECT SOFTWARE. THIS AGREEMENT DOES NOT, IN ANY MANNER, CONSTITUTE AN
# ENDORSEMENT BY GOVERNMENT AGENCY OR ANY PRIOR RECIPIENT OF ANY RESULTS,
# RESULTING DESIGNS, HARDWARE, SOFTWARE PRODUCTS OR ANY OTHER APPLICATIONS
# RESULTING FROM USE OF THE SUBJECT SOFTWARE.  FURTHER, GOVERNMENT AGENCY
# DISCLAIMS ALL WARRANTIES AND LIABILITIES REGARDING THIRD-PARTY SOFTWARE,
# IF PRESENT IN THE ORIGINAL SOFTWARE, AND DISTRIBUTES IT "AS IS."
#
# Waiver and Indemnity:  RECIPIENT AGREES TO WAIVE ANY AND ALL CLAIMS AGAINST
# THE UNITED STATES GOVERNMENT, ITS CONTRACTORS AND SUBCONTRACTORS, AS WELL
# AS ANY PRIOR RECIPIENT.  IF RECIPIENT'S USE OF THE SUBJECT SOFTWARE RESULTS
# IN ANY LIABILITIES, DEMANDS, DAMAGES, EXPENSES OR LOSSES ARISING FROM SUCH
# USE, INCLUDING ANY DAMAGES FROM PRODUCTS BASED ON, OR RESULTING FROM,
# RECIPIENT'S USE OF THE SUBJECT SOFTWARE, RECIPIENT SHALL INDEMNIFY AND HOLD
# HARMLESS THE UNITED STATES GOVERNMENT, ITS CONTRACTORS AND SUBCONTRACTORS,
# AS WELL AS ANY PRIOR RECIPIENT, TO THE EXTENT PERMITTED BY LAW.
# RECIPIENT'S SOLE REMEDY FOR ANY SUCH MATTER SHALL BE THE IMMEDIATE,
# UNILATERAL TERMINATION OF THIS AGREEMENT.
#
###############################################################################

if (NOT MPFR_FOUND)
  set(MPFR_ROOT "" CACHE PATH "Path to mpfr install directory")

  find_path(MPFR_INCLUDE_DIR
    NAMES mpfr.h
    HINTS "${MPFR_ROOT}/include"
    DOC "Path to mpfr include directory"
  )

  find_library(MPFR_LIB
    NAMES mpfr
    HINTS "${MPFR_ROOT}/lib"
    DOC "Path to mpfr library"
  )

  if (MPFR_INCLUDE_DIR)
    # Detect the version using mpfr.h
    file(READ "${MPFR_INCLUDE_DIR}/mpfr.h" MPFR_HEADER)

    if (MPFR_HEADER MATCHES "define[ \t]+MPFR_VERSION_MAJOR[ \t]+([0-9]+)")
      set(MPFR_MAJOR_VERSION "${CMAKE_MATCH_1}")
    else()
      message(FATAL_ERROR "could not find MPFR_VERSION_MAJOR in ${MPFR_INCLUDE_DIR}/mpfr.h")
    endif()

    if (MPFR_HEADER MATCHES "define[ \t]+MPFR_VERSION_MINOR[ \t]+([0-9]+)")
      set(MPFR_MINOR_VERSION "${CMAKE_MATCH_1}")
    else()
      message(FATAL_ERROR "could not find MPFR_VERSION_MINOR in ${MPFR_INCLUDE_DIR}/mpfr.h")
    endif()

    if (MPFR_HEADER MATCHES "define[ \t]+MPFR_VERSION_PATCHLEVEL[ \t]+([0-9]+)")
      set(MPFR_PATCHLEVEL_VERSION "${CMAKE_MATCH_1}")
    else()
      message(FATAL_ERROR "could not find MPFR_VERSION_PATCHLEVEL in ${MPFR_INCLUDE_DIR}/mpfr.h")
    endif()

    set(MPFR_VERSION "${MPFR_MAJOR_VERSION}.${MPFR_MINOR_VERSION}.${MPFR_PATCHLEVEL_VERSION}")
  endif()

  include(FindPackageHandleStandardArgs)
  find_package_handle_standard_args(MPFR
    REQUIRED_VARS
      MPFR_INCLUDE_DIR
      MPFR_LIB
      MPFR_VERSION
    VERSION_VAR
      MPFR_VERSION
    FAIL_MESSAGE
      "Could NOT find MPFR. Please provide -DMPFR_ROOT=/path/to/mpfr")
endif()
